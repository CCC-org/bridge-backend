buildscript {
	group "ccc"
	version "0.0.1-SNAPSHOT"

	ext {
		springBootVersion = "2.6.3"
		kotlinVersion = "1.8.20"
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
		classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
		classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'java'
	id 'com.epages.restdocs-api-spec' version '0.16.2'
}

allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: "kotlin"
	apply plugin: "kotlin-allopen"
	apply plugin: "kotlin-spring"
	apply plugin: "kotlin-jpa"
	apply plugin: "kotlin-noarg"
	apply plugin: "kotlin-kapt"
	apply plugin: "idea"

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	sourceSets {
		main.java.srcDir "src/main/java"
		main.kotlin.srcDir "src/main/kotlin"
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		implementation 'org.springframework.boot:spring-boot-starter-aop'
		implementation 'net.logstash.logback:logstash-logback-encoder:6.3'

		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.mockito:mockito-inline:4.6.1'
	}

	ext {
		springCloudVersion = "2021.0.1"
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
		exclude 'ccc/keewecore/**'
		exclude 'ccc/keewedomain/**'
		exclude 'ccc/keeweevent/**'
	}

	kapt {
		useBuildCache = false
		keepJavacAnnotationProcessors = true
	}
}

project(':keewe-batch') {
	dependencies {
		implementation project(path: ':keewe-domain', configuration: 'default')
		implementation 'org.springframework.boot:spring-boot-starter-batch'
		testImplementation 'org.springframework.batch:spring-batch-test'
	}
}

project(':keewe-push') {
	dependencies {
		implementation project(path: ':keewe-core', configuration: 'default')
		implementation project(path: ':keewe-domain', configuration: 'default')
		implementation 'io.github.jav:expo-server-sdk:1.1.0'
		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'io.netty:netty-resolver-dns-native-macos:4.1.68.Final:osx-aarch_64'
	}
}

project(':keewe-statistics') {
	dependencies {
		implementation project(path: ':keewe-domain', configuration: 'default')
	}
}

project(':keewe-infra') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	dependencies {
		implementation project(path: ':keewe-core', configuration: 'default')
		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
		implementation 'com.amazonaws:aws-java-sdk-s3:1.12.362'
		implementation group: 'org.imgscalr', name: 'imgscalr-lib', version: '4.2'
		implementation 'io.jsonwebtoken:jjwt:0.9.1'
	}
}

project(':keewe-event') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	dependencies {
		implementation project(path: ':keewe-core', configuration: 'default')
		implementation project(path: ':keewe-domain', configuration: 'default')
	}

	task prepareKotlinBuildScriptModel {
	}
}

project(':keewe-core') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-amqp'
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	}
}

project(':keewe-domain') {
	apply plugin: 'com.ewerk.gradle.plugins.querydsl'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	ext {
		queryDslVersion = "5.0.0"
	}

	dependencies {
		implementation project(path: ':keewe-infra', configuration: 'default')
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'mysql:mysql-connector-java'
		implementation group: 'com.h2database', name: 'h2', version: '2.1.214'
		implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8'
		implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
		implementation "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
		kapt "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
		testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'
	}

	def kaptMainDir = "$buildDir/generated/source/kapt/main"

	querydsl {
		jpa = true
		querydslSourcesDir = kaptMainDir
	}

	compileQuerydsl {
		options.annotationProcessorPath = configurations.querydsl
	}

	compileKotlin.dependsOn compileQuerydsl

	configurations {
		querydsl {
			extendsFrom compileClasspath
		}
	}
}

project(':keewe-api') {
	apply plugin: 'com.epages.restdocs-api-spec'

	ext {
		logbackVersion = "1.2.11"
		sentryVersion = "5.7.4"
	}

	dependencies {
		implementation project(path: ':keewe-domain', configuration: 'default')
		implementation project(path: ':keewe-event', configuration: 'default')
		implementation project(path: ':keewe-push', configuration: 'default')
		implementation "io.sentry:sentry-spring-boot-starter:${sentryVersion}"
		implementation "io.sentry:sentry-logback:${sentryVersion}"
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-security'
		implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
		implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
		testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.16.2'
		testImplementation 'org.springframework.security:spring-security-test'
	}

	def active = System.properties.getOrDefault('spring.profiles.active', 'local')

	task copyDocument(type:Copy) {
		from "build/api-spec"
		into "src/main/resources/static/docs/"
	}

	def makeApiDocs = tasks.register('makeApiDocs')
	makeApiDocs.configure {
		finalizedBy('openapi3').finalizedBy(copyDocument)
	}

	bootJar {
		if(active == 'local') {
			finalizedBy(makeApiDocs) // for local
		}
	}

	test {
		doFirst {
			delete file('build/generated-snippets')
			delete file('build/api-spec')
		}
		finalizedBy(makeApiDocs)
	}

	openapi3 {
		ext {
			local = 'http://localhost:8080'
			dev = 'https://api-keewe.com'
		}
		server = ext.get(active)
		title = 'Keewe-API'
		description = 'Keewe의 API 문서입니다.'
		format = 'yaml'
	}
}

bootJar {
	enabled = false
}
jar {
	enabled = true
}