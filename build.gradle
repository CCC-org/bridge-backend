plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'com.epages.restdocs-api-spec' version '0.16.2'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'ccc'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	ext {
		springCloudVersion = "2021.0.1"
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

}

project(':bridge-batch') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-batch'
		implementation 'org.springframework.boot:spring-boot-starter-quartz'
	}
}

project(':bridge-push') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-amqp'
	}
}


project(':bridge-statistics') {
	dependencies {
		implementation project(path: ':bridge-domain', configuration: 'default')
		implementation 'org.springframework.boot:spring-boot-starter-web'
	}
}

project(':bridge-infra') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
		// todo :: amqp import, aws config
	}
}


project(':bridge-domain') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	dependencies {
		implementation project(path: ':bridge-infra', configuration: 'default')
		// todo ::jpa, redis import
	}
}


project(':bridge-api') {
	apply plugin: 'com.epages.restdocs-api-spec'

	dependencies {
		implementation project(path: ':bridge-domain', configuration: 'default')
		implementation 'org.springframework.boot:spring-boot-starter-web'

		testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
		testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.16.2'
	}

	task copyDocument(type:Copy) {
		dependsOn('openapi3')

		from "build/api-spec"
		into "src/main/resources/static/docs/"
	}

	build {
		dependsOn copyDocument
	}

	bootJar {
		dependsOn('openapi3')
		from ("build/api-spec") {
			into 'static/docs'
		}
	}

	test.doFirst {
		delete file('build/generated-snippets')
	}

	openapi3 {
		ext {
			local = 'http://localhost:8080'
			dev = 'http://3.39.100.45:8080'
		}

		def active = System.properties.getOrDefault('spring.profiles.active', 'local')

		server = ext.get(active)
		title = 'Bridge-API'
		description = 'Bridge의 API 문서입니다.'
		format = 'yaml'
	}
}